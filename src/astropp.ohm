AstroPlusPlus {
  Program     = Stmt+
  Stmt        = Assignment
              | Print
              | IfStmt
              | WhileStmt
              | BreakStmt
  Assignment  = id "=" Exp ";"
  Print       = print Exp ";"
  IfStmt      = if Exp Block ElsePart?
  ElsePart    = else IfStmt
              | else Block
  WhileStmt   = while Exp Block
  Block       = "{" Stmt+ "}"
  BreakStmt   = break ";"
  
  Exp         = Exp1 relop Exp1                        --comparison
              | Exp1
  Exp1        = Exp1 ("+" | "-") Term                  --binary
              | Term
  Term        = Term ("*" | "/" | "%") Factor          --binary
              | Factor
  Factor      = Primary "**" Factor                    --binary
              | "-" Primary                            --negation
              | Primary
  Primary     = id "(" ListOf<Exp, ","> ")"            --call
              | numeral                                --num
              | id                                     --id
              | "(" Exp ")"                            --parens
  
  relop       = "<=" | "<" | "==" | "!=" | ">=" | ">" 
  numeral     = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
  print       = "print" ~idchar
  break       = "break" ~idchar
  if          = "if" ~idchar
  else        = "else" ~idchar
  while       = "while" ~idchar
  keyword     = print | break | if | else | while
  idchar      = letter | digit | "_"
  id          = ~keyword letter idchar*
  space      += "//" (~"\n" any)*                      --comment
}
